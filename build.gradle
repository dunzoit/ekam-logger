import groovy.lang.Closure
import org.gradle.api.publish.maven.MavenPublication
import org.gradle.api.tasks.bundling.Jar
import org.gradle.api.tasks.testing.Test

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "com.google.cloud.artifactregistry.gradle-plugin" version "2.1.5"
}

group 'com.testvagrant.ekam'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // logging
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-core:1.2.5'
    implementation 'ch.qos.logback:logback-classic:1.2.5'

    // test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}


task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'dunzoit'
            artifactId 'ekam-logger'
            version '1.0.0'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url "artifactregistry://asia-south1-maven.pkg.dev/dev-application-294611/maven-qe"
//            credentials {
//                username System.getenv("sonatypeUsername")
//                password System.getenv("sonatypePassword")
//                username sonatypeUsername
//                password sonatypePassword
//            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Ekam-logger, Logger for Ekam and related projects'
            name 'Ekam Logger'
            url 'https://github.com/dunzoit/ekam-logger'
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/dunzoit/ekam-logger/-/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/dunzoit/ekam-logger'
                connection 'https://github.com/dunzoit/ekam-logger.git'
                developerConnection 'git@github.com:dunzoit/ekam-logger.git'
            }
            developers {
                developer {
                    name 'Central QA'

                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}

task runTests(type: Test) {
    useTestNG()
}
